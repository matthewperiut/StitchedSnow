plugins {
    id 'fabric-loom' version '1.13.3'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url = 'https://maven.fabricmc.net/' }
    maven { url "https://maven.terraformersmc.com" } // modmenu
    maven { url "https://maven.shedaniel.me" } // rei
}

loom {
    splitEnvironmentSourceSets()

    mods {
        modid {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
        exclude(module: "fabric-gametest-api-v1")
    }

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
    // TODO 2025-11-06 (solonovamax): Disabled until fabric seasons is updated
    // modCompileOnly "io.github.lucaargolo:fabric-seasons:${project.seasons_version}"
    // modRuntimeOnly "io.github.lucaargolo:fabric-seasons:${project.seasons_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    withSourcesJar()
}


tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    options.release = 21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

project.afterEvaluate {
    loom {
        runs {
            configureEach {
                property("fabric.development=true")
                property("mixin.hotSwap")
                def mixinJarFile = configurations.compileClasspath.files {
                    it.group == "net.fabricmc" && it.name == "sponge-mixin"
                }.first()
                vmArg("-javaagent:$mixinJarFile")

                ideConfigGenerated(true)
            }
        }
    }
}
